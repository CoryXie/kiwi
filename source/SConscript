# Kiwi build system
# Copyright (C) 2009 Alex Smith
#
# Kiwi is open source software, released under the terms of the Non-Profit
# Open Software License 3.0. You should have received a copy of the
# licensing information along with the source code distribution. If you
# have not received a copy of the license, please refer to the Kiwi
# project website.
#
# Please note that if you modify this file, the license requires you to
# ADD your name to the list of contributors. This boilerplate is not the
# license itself; please refer to the copy of the license you have received
# for complete terms.

import tarfile, glob, os, tempfile, shutil

Import('config', 'envmgr')

SConscript(dirs=['boot/%s' % (config['ARCH']), 'kernel', 'uspace'])

# Create a TAR archive containing kernel modules, drivers and startup binaries.
def boot_image_func(target, source, env):
	# Create the work directory.
	tmpdir = tempfile.mkdtemp('.kiwibootimg')
	os.makedirs(os.path.join(tmpdir, 'applications'))
	os.makedirs(os.path.join(tmpdir, 'modules'))

	# Copy the startup binary, modules and other boot apps into it.
	shutil.copy(str(env['STARTUP']), tmpdir)
	for module in env['MODULES']:
		shutil.copy(str(module), os.path.join(tmpdir, 'modules'))
	for app in env['BOOTAPPS']:
		shutil.copy(str(app), os.path.join(tmpdir, 'applications'))

	# Create the TAR file.
	tar = tarfile.open(str(target[0]), 'w:gz')
	cwd = os.getcwd()
	os.chdir(tmpdir)
	for f in glob.glob('*'):
		tar.add(f)
	os.chdir(cwd)
	tar.close()

	# Clean up.
	shutil.rmtree(tmpdir)

dist = envmgr['dist']
dist['BUILDERS']['BootImage'] = Builder(action=Action(boot_image_func, '$GENCOMSTR'))

dist.BootImage('bootimg.tar.gz', [dist['STARTUP']] + dist['MODULES'] + dist['BOOTAPPS'])
dist['BOOTIMG'] = File('bootimg.tar.gz')
Default(Alias('bootimg', dist['BOOTIMG']))
