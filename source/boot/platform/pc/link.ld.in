/*
 * Copyright (C) 2010 Alex Smith
 *
 * Kiwi is open source software, released under the terms of the Non-Profit
 * Open Software License 3.0. You should have received a copy of the
 * licensing information along with the source code distribution. If you
 * have not received a copy of the license, please refer to the Kiwi
 * project website.
 *
 * Please note that if you modify this file, the license requires you to
 * ADD your name to the list of contributors. This boilerplate is not the
 * license itself; please refer to the copy of the license you have received
 * for complete terms.
 */

/**
 * @file
 * @brief		PC bootloader linker script.
 */

#include <arch/page.h>
#include <platform/boot.h>

OUTPUT_FORMAT("elf32-i386")
OUTPUT_ARCH("i386")

SECTIONS {
	. = LOADER_LOAD_ADDR;
	__start = .;

	.text : {
		__text_start = .;
		*(.init.text)
		*(.text)
		*(.text.*)

		__ap_trampoline_start = .;
		*(.trampoline)
		__ap_trampoline_end = .;

		__text_end = .;
	}

	.rodata ALIGN(32) : {
		__rodata_start = .;
		*(.rodata)
		*(.rodata.*)
		__rodata_end = .;
	}

	.data ALIGN(32) : {
		__data_start = .;
		*(.data)
		*(.data.*)
		__data_end = .;
	}

	.bss ALIGN(32) : {
		__bss_start = .;
		*(.bss)
		*(.bss.*)
		*(COMMON)
		__bss_end = .;
	}

	. = ALIGN(PAGE_SIZE);
	__end = .;

	/DISCARD/ : {
		*(.note.GNU-stack)
		*(.comment*)
	}
}
