# Kiwi build system
# Copyright (C) 2009 Alex Smith
#
# Kiwi is open source software, released under the terms of the Non-Profit
# Open Software License 3.0. You should have received a copy of the
# licensing information along with the source code distribution. If you
# have not received a copy of the license, please refer to the Kiwi
# project website.
#
# Please note that if you modify this file, the license requires you to
# ADD your name to the list of contributors. This boilerplate is not the
# license itself; please refer to the copy of the license you have received
# for complete terms.

import os
import sys

Import('env', 'sources')

# Set build flags to prevent FPU code generation.
env['CCFLAGS'] += ['-mno-sse', '-mno-mmx', '-mno-sse2', '-mno-3dnow']

# Generate the AP boot code.
env.Object('ap_boot.S')
env.Command('ap_boot.bin.elf', ['ap_boot.o'], Action('$LINK -Ttext=0 -o $TARGET $SOURCE', None))
env.Command('ap_boot.bin', ['ap_boot.bin.elf'], Action('$OBJCOPY -O binary $SOURCE $TARGET', None))
env.Command('ap_boot.bin.c', ['ap_boot.bin'],
            Action('$BIN2HEX --no-size --section .init.trampoline $SOURCE __smp_trampoline > $TARGET',
                   '$GENCOMSTR'))

# Add source files.
sources += [
	File('ap_boot.bin.c'),
	File('cpu.c'),
	File('fpu.c'),
	File('intr.c'),
	File('kdbg.c'),
	File('lapic.c'),
	File('smp.c'),
	File('vm.c'),
]
