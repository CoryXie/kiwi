# Copyright (C) 2010 Alex Smith
#
# Kiwi is open source software, released under the terms of the Non-Profit
# Open Software License 3.0. You should have received a copy of the
# licensing information along with the source code distribution. If you
# have not received a copy of the license, please refer to the Kiwi
# project website.
#
# Please note that if you modify this file, the license requires you to
# ADD your name to the list of contributors. This boilerplate is not the
# license itself; please refer to the copy of the license you have received
# for complete terms.

import os.path
Import('env', 'envmgr')

envmgr.base['KRPCGEN'] = env.Program('krpcgen', [
	'CXXCodeGen.cc',
	'Function.cc',
	'Service.cc',
	'Type.cc',
	'parser.y',
	'lexer.l',
	'main.cc',
], CXXFLAGS=env['CXXFLAGS'] + env['CCFLAGS'], CCFLAGS=[], CFLAGS=[])

# Add builders to generate RPC server/client code.
def rpc_emitter(target, source, env):
	target.append(File(os.path.splitext(str(target[0]))[0] + '.h'))
	Depends(target[0], env['KRPCGEN'])
	return target, source
envmgr.base['BUILDERS']['RPCServer'] = Builder(
	action=Action('$KRPCGEN -s $TARGET $SOURCE', '$GENCOMSTR'),
	emitter=rpc_emitter
)
envmgr.base['BUILDERS']['RPCClient'] = Builder(
	action=Action('$KRPCGEN -c $TARGET $SOURCE', '$GENCOMSTR'),
	emitter=rpc_emitter
)
