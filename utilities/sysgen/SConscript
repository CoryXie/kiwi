# Copyright (C) 2010 Alex Smith
#
# Kiwi is open source software, released under the terms of the Non-Profit
# Open Software License 3.0. You should have received a copy of the
# licensing information along with the source code distribution. If you
# have not received a copy of the license, please refer to the Kiwi
# project website.
#
# Please note that if you modify this file, the license requires you to
# ADD your name to the list of contributors. This boilerplate is not the
# license itself; please refer to the copy of the license you have received
# for complete terms.

import os.path
Import('config', 'env', 'envmgr')

envmgr.base['SYSGEN'] = env.Program('sysgen', [
	'AMD64Target.cc',
	'IA32Target.cc',
	'parser.y',
	'lexer.l',
	'main.cc',
], CXXFLAGS=env['CXXFLAGS'] + env['CCFLAGS'], CCFLAGS=[], CFLAGS=[])

# Add builders to generate system call code.
def sysgen_emitter(target, source, env):
	source.insert(0, env['SYSINC'])
	env.Depends(target[0], env['SYSGEN'])
	return target, source
envmgr.base['BUILDERS']['Sysgen'] = Builder(
	action=Action('$SYSGEN -o $TARGET %s $SOURCES' % (config['ARCH']), '$GENCOMSTR'),
	emitter=sysgen_emitter
)
